// The Hexa Compiler
// Copyright (C) 2018  Oleg Petrenko
// Copyright (C) 2018  Bogdan Danylchenko
//
// This library is free software; you can redistribute it and/or
// modify it under the terms of the GNU Lesser General Public
// License as published by the Free Software Foundation; either
// version 2.1 of the License, or (at your option) any later version.
//
// This library is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
// Lesser General Public License for more details.
//
// You should have received a copy of the GNU Lesser General Public
// License along with this library; if not, write to the Free Software
// Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301  USA

enum Generator: String {
	JavaScript = "JavaScript"
	C = "C"
	CSharp = "C#"
	Haxe = "Haxe"
	Hexa = "Hexa"
}

declare Target = {
	generator: Generator,
	instance: { generatePackageJson: Bool }
}

// Contents of hexa.json
declare Package = {
	name: String,
	output: String,
	version: String,
	description: String,
	author: String,
	license: String,
	target: Target,
	files: [String]
}

class Main {
	// Source https://stackoverflow.com/questions/3430939/node-js-readsync-from-stdin
	static function question(query: String): String {
		Process.stdout.write(query)
		var bufsize = 256
		var buf = new Buffer(bufsize)
		var bytesRead = 0
		try {
			bytesRead = Fs.readSync(Process.stdin.fd, buf, 0, bufsize)
		} catch (e: { code: String }) {{
			if (e.code == 'EAGAIN') return 'ERROR: interactive stdin input not supported.'
			if (e.code == 'EOF') return ''
			throw e // unexpected exception
		}}
		if (bytesRead == 0) {
			// No more stdin input available.
			// OS X 10.8.3: regardless of input method, this is how the end
			// of input is signaled.
			// Windows 7: this is how the end of input is signaled for
			// *interactive* stdin input.
			return ''
		}
		// Process the chunk read.
		let answer = buf.toString('utf8', 0, bytesRead).trim()
		return answer
	}

	// Asks a couple of questions and creates hexa.json
	function init() {
		var currentDirTmp = Process.argv[1].split(Path.sep)
		var packageFileName = currentDirTmp.splice(0, -1).join(Path.sep) + Path.sep + "hexa.json"
		var currentDir = currentDirTmp[currentDirTmp.length - 2]
		var name = question('Enter package name (\(currentDir)): ')
		if (name == "") name = currentDir

		var version = question("Enter package version (1.0.0): ")
		if (version == "") version = "1.0.0"

		var description = question("Enter package description: ")

		var main = question("Enter main file (main.hexa): ")
		if (main == "") main = "main.hexa"

		var target: Target = { generator: Generator.JavaScript, instance: { generatePackageJson: false } }
		var generatorstr = question("Enter generator type (JavaScript):\n1. JavaScript\n2. C\n3. Haxe")
		if (generatorstr.toUpperCase() == "C" || generatorstr == "2") target.generator = Generator.C
		else if (generatorstr.toUpperCase() == "HAXE" || generatorstr == "3") target.generator = Generator.Haxe

		while (!(main.endsWith(".hexa"))) {
			Process.stdout.write("Wrong file name. Must ends with '.hexa'.\n")
			main = question("Enter main file (main.hexa): ")
		}

		var author = question("Enter package author name: ")
		var license = question("Enter license: ")

		var pack: Package = {
			name: name, version: version, description: description,
			output: name,
			main: main, author: author, license: license, target: target, files: []
		}
		var packageStr = JSON.stringify(pack, null, "\t")
		var answ = question('About to write to: \(packageFileName):\n\(packageStr)\nIs this ok? (yes)')
		if (answ != "") Process.exit(1)

		Fs.writeFileSync("hexa.json", packageStr)
		Process.stdout.write("Hexa package initialized!\n")
	}

	// Parses a file
	function processFile(target: String): Node {
		let content = Fs.readFileSync(target)
		let tokens = Lexer.tokenize(content, target)
		let parsed = Parser.parseNodes(tokens)
		return parsed
	}

	// Read-eval-print loop, starts if hexa called without arguments
	function repl() {
		// Load standard library
		let js = processFile(__dirname + '/library/js/js.hexa')
		let nodejs = processFile(__dirname + '/library/nodejs/nodejs.hexa')
		// REPL
		let lines = []
		var lines = []
		while (true) {
			var line = question("> ")
			if (line.length == 0) continue
			lines.push(line)
			lines = [line] // Do not remember previous commands yet
			try {
				var code = '"use strict"\r\n'
				try {
					let content = lines.join('\n')
					let tokens = Lexer.tokenize(Buffer.from(content), "REPL")
					let parser = Parser.toNode(Parser.parseNodes(tokens))
					var collected = Node.TBlock([js, nodejs, parser])
					if (parser == null) continue
					Typer.fillScopes(collected)
					code += GenJs.stringifyProject(Normalizer.normalize(collected, GenJs.reserved))
				} catch (e: Any) {
					lines.pop()
					console.error('\x1b[31mCompilation Error: ' + e, '\x1b[0m')
					continue
				}
				let result = eval(code)
				if (result == null) console.log('\x1b[90m' + result, '\x1b[0m')
				else if (result == true) console.log('\x1b[33m' + result, '\x1b[0m')
				else if (result == false) console.log('\x1b[33m' + result, '\x1b[0m')
				else console.log(result)
			} catch (e: Any) {
				console.error("Error: " + e)
			}
		}
	}

	// Parses all files, shows errors and stops if any
	function processFiles(targets: [String]): [[Node]] {
		var res: [[Node]] = []
		for (target in targets) {
			try {
				res.push(processFile(target))
			} catch (e: Any) {
				console.log(e.toString())
			}
		}
		if (targets.length != res.length) throw 'Stopped on errors'
		return res
	}

	// Loads hexa.json
	function loadPackage(filename): Package {
		var pack = JSON.parse(Fs.readFileSync(Path.resolve(filename)).toString('utf8'))
		function incorrect(what: String) {
			Process.stdout.write("Incorrect package file! Field `\(what)` does not exists\n")
			Process.exit(1)
		}
		if (pack.target == null) incorrect('target')
		if (pack.output == null) incorrect('output')
		if (pack.name == null) incorrect('name')
		Project.defines = pack.define ?? {:}

		// Add standard library
		if (pack.files == null) pack.files = []

		switch (pack.target.generator) {
			case Generator.JavaScript:
				Project.defines.js = Project.defines.js ?? true
				if (pack.noLib == true) {
				} else {
					pack.files.unshift(Path.resolve(__dirname + '/library/js/js.hexa'))
					pack.files.unshift(Path.resolve(__dirname + '/library/nodejs/nodejs.hexa'))
				}
			case Generator.C:
				Project.defines.c = Project.defines.c ?? true
				Project.defines.native = Project.defines.native ?? true
			case Generator.Haxe:
				Project.defines.haxe = Project.defines.haxe ?? true
			case Generator.Hexa:
				Project.defines.hexa = Project.defines.hexa ?? true
				if (pack.noLib == true) {
				} else {
					pack.files.unshift(Path.resolve(__dirname + '/library/js/js.hexa'))
					pack.files.unshift(Path.resolve(__dirname + '/library/nodejs/nodejs.hexa'))
				}
			case Generator.CSharp:
				Project.defines.cs = Project.defines.cs ?? true
				pack.files.unshift(Path.resolve(__dirname + '/library/cs/cs.hexa'))
		}
		return pack
	}

	// Entry
	new() {
		// Debug information

		// Used to measure compiler performance and to show a success message
		let begin = Date.now()

		// Initialize compiler
		Lexer.init()

		// Exit before self-test
		if (Process.argv[2] == "--syntax-linter") {
			if (Process.argv.length < 4) {
				console.log("Usage: `node hexa.js --syntax-linter file.hexa`")
			} else {
				let file = Process.argv[3]
				try {
					let content = Fs.readFileSync(file)
					try {
						let tokens = Lexer.tokenize(content, file)
						try {
							let parser = Parser.parseNodes(tokens)
						} catch (e: Any) console.log("\(e)")
					} catch (e: Any) console.log("\(e)")
				} catch (e: Any) console.log("ERROR: Unable to open file \(file)")
			}
			Process.exit(0)
		}

		// Self-test
		#if debug
			#if times
				let beginTests = Date.now()
			#end
			TestLexer.test()
			TestParser.test()
			TestTyper.test()
			#if times
				console.log("[Tests finished in \(Date.now() - beginTests) ms]")
			#end
		#end

		// Usage examples
		if (Process.argv[2] == "--version") {
			console.log('Hexa Alpha on node.js ' + Process.versions.node)
			Process.exit(0)
		}
		else if (Process.argv[2] == "--help") {
			console.log("Usage: `node hexa.js project.json` or `node hexa.js init`")
			Process.exit(1)
		}
		else if (Process.argv[2] == null) {
			repl()
			Process.exit(0)
		}
		else if (Process.argv[2] == "init") {
			init()
			Process.exit(0)
		}

		// Get inputs
		var currentFile = Path.resolve(Process.argv[2])
		var currentParsedFile: ParsedPath = Path.parse(currentFile)
		var input: Package? = null
		var packageFolder: String = currentParsedFile.dir

		// Build the selected project
		if (currentParsedFile.ext == ".json") {
			input = loadPackage(currentFile)
			console.log("[Building \(input.name)]")
		}
		// Search for project in the selected folder
		else if (Fs.lstatSync(currentFile).isDirectory()) {
			input = loadPackage(currentFile + Path.sep + "hexa.json")
			packageFolder = currentFile
		}
		// Evaluate file directly
		else if (currentParsedFile.ext == ".hexa") {
			let code = '"use strict"\r\n' + GenJs.stringifyProject(Normalizer.normalize(Node.TBlock([processFile(currentFile)]), GenJs.reserved))
			eval(code)
			return
		}

		// Perform compilation
		#if times
			let beginParsing = Date.now()
		#end

		var files: [[Node]] = []
		if (input.files != null) {
			let fileList = input.files.map(function(file) {
				// Avoid global paths
				if (file.startsWith('/') || file.charAt(1) == ':') return Path.resolve(file)
				return Path.resolve(packageFolder + Path.sep + file)
			})
			files = processFiles(fileList)
		}

		#if times
			console.log("[Parsing finished in \(Date.now() - beginParsing) ms]")
			let beginTyper = Date.now()
		#end
		try {
		} catch (e: Any) {
			console.log(e.toString())
			Process.exit(1)
		}
			Typer.fillScopes(files)
		#if times
			console.log("[Typer finished in \(Date.now() - beginTyper) ms]")
		#end


		// Perform code generation
		switch (input.target.generator) {
			case Generator.JavaScript:
				var target = packageFolder + Path.sep + Path.parse(input.output).name + ".js"
				let license = input.license != null? "// LICENSE \(input.license)\r\n" : ''

				#if times
					let beginNormalizer = Date.now()
				#end

				let normal = Normalizer.normalize(files, GenJs.reserved)

				#if times
					console.log("[Normalizer finished in \(Date.now() - beginNormalizer) ms]")
					let beginGenJs = Date.now()
				#end

				let outs = GenJs.stringifyProject(normal)

				#if times
					console.log("[GenJs finished in \(Date.now() - beginGenJs) ms]")
				#end

				Fs.writeFileSync(target, license + outs)
			case Generator.C:
				var target = packageFolder + Path.sep + Path.parse(input.output).name + ".c"
				let license = input.license != null? "// LICENSE \(input.license)\r\n" : ''

				#if times
					let beginNormalizer = Date.now()
				#end

				let normal = Normalizer.normalize(files, GenC.reserved)

				#if times
					console.log("[Normalizer finished in \(Date.now() - beginNormalizer) ms]")
					let beginGenC = Date.now()
				#end

				let outs = GenC.stringifyProject(normal)

				#if times
					console.log("[GenC finished in \(Date.now() - beginGenC) ms]")
				#end

				Fs.writeFileSync(target, license + outs)
			case Generator.Haxe:
				var target = packageFolder + Path.sep + Path.parse(input.output).name + ".hx"
				let license = input.license != null? "// LICENSE \(input.license)\r\n" : ''

				#if times
					let beginNormalizer = Date.now()
				#end

				let normal = Normalizer.normalize(files, GenHaxe.reserved, nativeEnums: true)

				#if times
					console.log("[Normalizer finished in \(Date.now() - beginNormalizer) ms]")
					let beginGenJs = Date.now()
				#end

				let outs = GenHaxe.stringifyProject(normal)

				#if times
					console.log("[GenHaxe finished in \(Date.now() - beginGenJs) ms]")
				#end

				Fs.writeFileSync(target, license + outs)
			case Generator.Hexa:
				var target = packageFolder + Path.sep + Path.parse(input.output).name + ".hexa"
				let license = input.license != null? "// LICENSE \(input.license)\r\n" : ''

				#if times
					let beginNormalizer = Date.now()
				#end

				let normal = Normalizer.normalize(files, GenHexa.reserved, nativeEnums: true)

				#if times
					console.log("[Normalizer finished in \(Date.now() - beginNormalizer) ms]")
					let beginGenHexa = Date.now()
				#end

				let outs = GenHexa.stringifyProject(normal)

				#if times
					console.log("[GenHexa finished in \(Date.now() - beginGenHexa) ms]")
				#end

				Fs.writeFileSync(target, license + outs)
			case Generator.CSharp:
				var target = packageFolder + Path.sep + Path.parse(input.output).name + ".cs"
				let license = input.license != null? "// LICENSE \(input.license)\r\n" : ''

				#if times
					let beginNormalizer = Date.now()
				#end

				let normal = Normalizer.normalize(files, GenCSharp.reserved, nativeEnums: false)

				#if times
					console.log("[Normalizer finished in \(Date.now() - beginNormalizer) ms]")
					let beginGenCSharp = Date.now()
				#end

				let outs = GenCSharp.stringifyProject(normal)

				#if times
					console.log("[GenCSharp finished in \(Date.now() - beginGenCSharp) ms]")
				#end

				Fs.writeFileSync(target, license + outs)
			case _: throw "Generator is not defined or unknown. Is there `target.generator` field in `hexa.json`?"
		}

		console.log("[Finished in \(Date.now() - begin) ms]")
	}
}

// Entry point
new Main()
