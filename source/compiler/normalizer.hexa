// The Hexa Compiler
// Copyright (C) 2019  Oleg Petrenko
//
// This library is free software; you can redistribute it and/or
// modify it under the terms of the GNU Lesser General Public
// License as published by the Free Software Foundation; either
// version 2.1 of the License, or (at your option) any later version.
//
// This library is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
// Lesser General Public License for more details.
//
// You should have received a copy of the GNU Lesser General Public
// License along with this library; if not, write to the Free Software
// Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301  USA

module {
	/// Generates simplified view of syntax tree
	/// for easier later generation
	/// See: `/docs/normalizer.md`
	class Normalizer {
		let project = new NiceProject()
		var keywords: [String] = []
		var reservedGlobals: [String] = []
		var collected: [[Node]] = []
		var nativeEnums = false
		var globalAccessor = '<!-- global -->'
		var typer: Typer
		var package: Package

		new () {
		}

		function normalize() {
			// Reserve globals
			for (global in reservedGlobals) addToScope(global)

			// Rename upfront to avoid race condition
			for (file in collected) for (e in file) switch (e) {
				case Node.TModule(path, el):
					for (e in el) switch (e) {
						case Node.TClass(t, ext, impl, fields, external):
							renameClassFields(fields)
						case Node.TEnum(t, fields, valuesType):
							renameClassFields(fields)
						case Node.TVar(name, t, expr, const, external):
							let atts = Project.mapAttributes.get(e)
							let native = getAtt(atts, 'native')
							if (native != null) switch (native.values[0]) {
								case Node.TString(s): uniqueNative(s, e)
								case _: throw '@native takes string as argument'
							} else unique(name, e)
						case Node.TFunction(name, expr, vars, rettype, external):
							if (let name = name) unique(name, e)
					}
			}

			// Fill statements
			let init = []
			for (file in collected) {
				let initial = []
				pushScope()
				for (e in file) {
					switch (e) {
						case Node.TModule(path, el):
							pushScope()
							for (e in el) switch (e) {
								case Node.TFunction(_):
									if (let s = nodeToStatement(e)) // Non-declare ones
										project.globalFuncs.push(s)
								case Node.TVar(_):
									if (let s = nodeToStatement(e)) // Non-declare ones
										project.globalVars.push(s)
								case Node.TVars(vars):
									for (v in vars)
										if (let s = nodeToStatement(v)) // Non-declare ones
											project.globalVars.push(s)
								case _:
									fillStatement(e, path)
							}
							popScope()
						case _:
							let statement = fillStatement(e, null)
							switch (statement) {
								case null:
								case NiceStatement.Block(el):
								if (el.length > 0) // Don't output empty blocks
									initial.push(statement)
								case _:
									initial.push(statement)
							}
					}
				}
				popScope()
				if (initial.length > 0) init.push(NiceStatement.Block(initial))
			}
			project.init.push(NiceStatement.Block(init))
			if (project.init.length > 1) throw "too many inits"
			project.package = package
			project.typer = typer

			// Sort non-inherited & external classes upfront
			// TODO backport implementation of dependency tree
			project.classes.sort(function(a, b) {
				if (a.external == true) return -1
				if (a.extend == null) return 0
			    return 1
			})

			return project
		}

		function nodeToExpression(e: Node): NiceExpression {
			if (e == null) return null

			switch (e) {
				// Transformation not required
				case Node.TString(v):
					return NiceExpression.String(v)
				case Node.TInt(v):
					return NiceExpression.Int(v)
				case Node.TFloat(v):
					return NiceExpression.Float(v)
				case Node.TBool(v):
					if (v) return NiceExpression.True
					return NiceExpression.False
				case Node.TNull:
					return NiceExpression.Null

				case Node.NodeTypeValue(t): switch (t) {
					case NodeType.Type(name):
						return NiceExpression.Ident(parentNames.get(typer.parents.get(e)) ?? name)
				}

				// Transform
				case Node.TIdent(name):
					var source = typer.parents.get(e)
					switch (source) {
						case null:
							throw 'Unmapped `\(e)` of name `\(name)` ' + JSON.stringify(Project.data.get(e))
						case Node.TVar(name, _, _):
							if (let map = typer.parents.get(source)) switch (map) {
								case Node.TModule(path, _):
									let name = parentNames.get(source) ?? name
									return NiceExpression.Ident(globalAccessor + name)
								case _:
									return NiceExpression.Dot(NiceExpression.This, name)
							}
						case Node.TFunction(name, _):
							if (let map = typer.parents.get(source)) switch (map) {
								case Node.TModule(path, _):
									return NiceExpression.Ident(globalAccessor + name)
								case _:
									return NiceExpression.Dot(NiceExpression.This, name)
							}
						case Node.TStatic(f):
							let staticSource = typer.parents.get(source)
							switch (staticSource) {
								case Node.TClass(t, _):
									return NiceExpression.Dot(NiceExpression.Ident(DataHelper.extractTypeName(t)), getClassDotField(staticSource, name))
								case Node.TEnum(t,_):
									return NiceExpression.Dot(NiceExpression.Ident(DataHelper.extractTypeName(t)), name)
								case _:
									throw 'staticSource is \(staticSource) for \(name)'
							}
					}
					return NiceExpression.Ident(parentNames.get(source) ?? name, typer.types.get(e))

				// `expr.name`
				case Node.TDot(expr, name):
				let parent = typer.parents.get(expr)
				switch (parent) {
					case Node.TEnum(t, fields, valuesType):
						if (!nativeEnums, let vt = valuesType, let tag = getTag(parent, name)) return tag
						return NiceExpression.Dot(nodeToExpression(expr), getClassDotField(parent, name) ?? name)
					case Node.TClass(_): return NiceExpression.Dot(nodeToExpression(expr), getClassDotField(parent, name) ?? name)
				}
				if (let type = typer.types.get(expr)) switch (type) {
					case Type.ClassInstance(type):
						let renamed = type.fieldRenames[type.fieldNames.indexOf(name)]
						if (let rename = renamed) {
							return NiceExpression.Dot(nodeToExpression(expr), rename)
						}
				}
				return NiceExpression.Dot(nodeToExpression(expr), name)

				case Node.TDotUpper(expr, name):
				let parent = typer.parents.get(expr)
				switch (parent) {
					case Node.TEnum(t, fields, valuesType):
						if (!nativeEnums, let vt = valuesType, let tag = getTag(parent, name))
							return tag
						return NiceExpression.Dot(nodeToExpression(expr), getClassDotField(parent, name) ?? name)
					case Node.TClass(_): return NiceExpression.Dot(nodeToExpression(expr), getClassDotField(parent, name) ?? name)
				}
				return NiceExpression.Dot(nodeToExpression(expr), name)

				case Node.TCall(e, args, argNames):

				// Optimize `'String'.charCodeAt(0)`
				if (args.length == 1) switch (e) {
					case Node.TDot(expr, name): if (name == 'charCodeAt') switch (expr) {
						case Node.TString(s): if (s.indexOf('\\') == -1) switch (args[0]) {
							case Node.TInt(v):
								let i = parseInt(v)
								if (i < s.length, i > -1) return NiceExpression.Int(s.charCodeAt(i))
						}
					}
				}

				let a = []
				for (arg in args) {
					a.push(nodeToExpression(arg))
				}

				// Enum constructor
				if (!nativeEnums) switch (e) {
					case Node.TDotUpper(expr, name):
						let parent = typer.parents.get(expr)
						switch (parent) {
							case Node.TEnum(t, fields, valuesType):
								if (let tag = getTag(parent, name))
									return NiceExpression.Enum(DataHelper.extractTypeName(t), name, tag, a)
						}
				}
				let atts = Project.mapAttributes.get(typer.parents.get(e))

				let infix = getAtt(atts, 'infix')
				if (infix != null) switch (infix.values[0]) {
					case Node.TString(s): return NiceExpression.NativeOperator(NativeOperatorKind.Infix, a, s)
					case _: throw '@infix takes string as argument'
				}

				let operator = getAtt(atts, 'operator')
				if (operator != null) switch (operator.values[0]) {
					case Node.TString(s): return NiceExpression.NativeOperator(NativeOperatorKind.Function, a, s)
					case _: throw '@operator takes string as argument'
				}

				let v = nodeToExpression(e)
				return NiceExpression.Call(v, a)

				case Node.TBinop(a, op, b):
				let ea = nodeToExpression(a)
				let eb = nodeToExpression(b)

				if (op == Token.OpAdd) switch (ea) {
					case NiceExpression.String(sa):  switch (eb) {
						case NiceExpression.String(sb): return NiceExpression.String(sa + sb)
					}
				}

				return NiceExpression.Binop(ea, op, eb, typer.types.get(e))

				case Node.TNew(path, t, args, fields, el):
				let v = NiceExpression.Ident(parentNames.get(typer.parents.get(e)) ?? DataHelper.extractTypeName(t))
				let a = []
				for (arg in args) {
					a.push(nodeToExpression(arg))
				}
				return NiceExpression.New(v, a)

				case Node.TFunction(name, expr, vars, rettype):
				let a = []
				let defaults = []
				let types = []
				for (arg in vars) switch (arg) {
					case Node.TVar(name, t, expr, const):
						a.push(unique(name, arg))
						types.push(t)
						if (expr != null) defaults.push(nodeToExpression(expr))
						else defaults.push(null)
				}
				return NiceExpression.Function(name!=null?unique(name, e):null, nodeToStatement(expr), a, defaults, typer.types.get(e))

				case Node.TArrow(expr, vars, rettype):
				let a = []
				let defaults = []
				let types = []
				for (arg in vars) switch (arg) {
					case Node.TVar(name, t, expr, const):
						a.push(unique(name, arg))
						types.push(t)
						if (expr != null) defaults.push(nodeToExpression(expr))
						else defaults.push(null)
				}
				return NiceExpression.Arrow(nodeToExpression(expr), a, defaults, types, rettype)

				case Node.TBlock(el):
				if (el.length == 1)
					return nodeToExpression(el[0])
				let block = []
				var i = 0
				while (i < el.length - 1) {
					if (let s = nodeToStatement(el[i])) block.push(s)
					i++
				}
				block.push(NiceStatement.Return(nodeToExpression(el[el.length - 1])))
				return NiceExpression.Call(NiceExpression.Parenthesis(NiceExpression.Function(null, NiceStatement.Block(block), [])), [])

				case Node.TObject(names, el):
				let els = []
				for (e in el) {
					els.push(nodeToExpression(e))
				}
				return NiceExpression.Object(names, els)

				case Node.TParenthesis(inner):
				function unwrapNestedParens(inner: Node) {
					switch (inner) {
						case Node.TParenthesis(i): return unwrapNestedParens(i)
						case _: return inner
					}
				}
				let value = unwrapNestedParens(inner)
				return NiceExpression.Parenthesis(nodeToExpression(value))

				case Node.TArray(el):
				if (el.length == 1) switch (el[0]) {
					case Node.TFor(name, over, by):
					let block = []
					let result = unique('result')
					block.push(NiceStatement.Const(result, NiceExpression.Array([])))
					let value = unique('value')
					block.push(NiceStatement.Const(value, nodeToExpression(over)))

					let push = NiceStatement.Call(NiceExpression.Dot(NiceExpression.Ident(result), 'push'), [nodeToExpression(by)])
					function fillLoop() {
						switch (over) {
							case Node.TInt(s):
							let i = name
							block.push(NiceStatement.Const(i, NiceExpression.Int(-1)))
							block.push(NiceStatement.While(
								NiceExpression.Binop(
									NiceExpression.Parenthesis(NiceExpression.Binop(NiceExpression.Ident(i), Token.OpAdd, NiceExpression.Int(1))),
									Token.OpLt, NiceExpression.Ident(value)),
								NiceStatement.Block([
									NiceStatement.Increment(NiceExpression.Ident(i)),
									push
								]),
								true))
							return
						}

						block.push(NiceStatement.For(name, NiceExpression.Ident(value), push))
					}

					fillLoop()
					block.push(NiceStatement.Return(NiceExpression.Ident(result)))
					return NiceExpression.Call(
						NiceExpression.Parenthesis(
							NiceExpression.Function(null, NiceStatement.Block(block), [], [], Type.Function([], typer.types.get(e)))
						)
					, [])
				}
				let els = []
				for (e in el) els.push(nodeToExpression(e))
				return NiceExpression.Array(els)

				// `nullable ?? otherwise`
				case Node.TElvis(nullable, otherwise):
					let result = unique('result')
					return NiceExpression.Call(NiceExpression.Parenthesis(NiceExpression.Function(null, NiceStatement.Block([
						NiceStatement.Const(result, nodeToExpression(nullable)),
						NiceStatement.If(NiceExpression.Binop(NiceExpression.Ident(result), Token.OpNotEq, NiceExpression.Null), NiceStatement.Return(NiceExpression.Ident(result)), null),
						NiceStatement.Return(nodeToExpression(otherwise))
					]), [], [], Type.Function([], typer.types.get(otherwise)))), [], typer.types.get(otherwise))
					return NiceExpression.Elvis(nodeToExpression(nullable), nodeToExpression(otherwise))

				case Node.TUnop(op, postfix, e):
					return NiceExpression.Unop(op, postfix, nodeToExpression(e))

				case Node.TThis:
					return NiceExpression.This

				case Node.TMap(keys, values):
				let k = []
				let v = []
				for (key in keys) k.push(nodeToExpression(key))
				for (value in values) v.push(nodeToExpression(value))
				return NiceExpression.Map(k, v)

				case Node.TAssignop(a, op, b):
				return NiceExpression.Assignop(nodeToExpression(a), op, nodeToExpression(b))

				case Node.TIf(econd, eif, eelse):
				// Check if it contains if-let bindings
				var binds = false
				for (cond in econd) switch (cond) {
					case Node.TVar(_):
					binds = true
				}

				if (!binds && econd.length == 1)
				return NiceExpression.If(nodeToExpression(econd[0]), nodeToExpression(eif), nodeToExpression(eelse))

				if (!binds) {
					var parens = NiceExpression.Parenthesis(nodeToExpression(econd[0]))
					var i = 1
					while (i < econd.length) {
						parens = NiceExpression.Binop(parens, Token.OpBoolAnd, NiceExpression.Parenthesis(nodeToExpression(econd[i])))
						i++
					}
					return NiceExpression.If(parens, nodeToExpression(eif), nodeToExpression(eelse))
				}

				return NiceExpression.If(nodeToExpression(econd[0]), nodeToExpression(eif), nodeToExpression(eelse))

				case Node.TIndex(expr, index):
				return NiceExpression.Index(nodeToExpression(expr), nodeToExpression(index))

				case Node.TAs(e, kind, t): switch (kind) {
					case Token.Question:
						console.log('`as?` is not yet supported by normalizer, only `as!`')
					case Token.OpNot:
						return NiceExpression.UnsafeBitCast(nodeToExpression(e), typer.types.get(t))
					case _:
						console.log('`as` is not yet supported by normalizer, only `as!`')
				}

				case Node.TIs(e, t):
					return NiceExpression.Is(nodeToExpression(e), typer.types.get(t))

				case _: console.error('(internal warning) Unknown node type for expression:', e, Project.data.get(e))
			}

			return null
		}

		function nodeToStatement(e: Node): NiceStatement {
			if (e == null) return null

			switch (e) {
				case Node.TBlock(el):
				var els = [] // TODO break after: break, continue, return, throw
				for(e in el) {
					let statement = nodeToStatement(e)
					if (statement != null) // Optimized away
					els.push(statement)
				}

				// Unwrap `{{{{block}}}}`
				if (els.length == 1) switch (els[0]) {
					case NiceStatement.Block(_):
						return els[0]
				}

				return NiceStatement.Block(els)

				// Types
				case Node.TEnum(t, fields, valuesType):
				renameClassFields(fields)
				let isComplexEnum = valuesType == null
				if (isComplexEnum) {
					let type = new NiceComplexEnum()
					type.name = unique(DataHelper.extractTypeName(t), e)
					globals.push(type.name)
					let names = []
					let constructors = []
					let types = []
					for (f in fields) switch (f) {
						case Node.NodeTypeValue(t): switch (t) {
							case NodeType.Type(name):
							names.push(name)
							constructors.push(null)
							types.push(null)
						}
						case Node.TCall(e, args, argNames):
							switch (e) {
								case Node.NodeTypeValue(t): switch (t) {
									case NodeType.Type(name): names.push(name)
								}
							}
							constructors.push([for (arg in argNames) unique(arg)])
							types.push([for (arg in args) unique(arg)])
						case Node.TStatic(field):
							switch (field) {
							case Node.TVar(name, t, expr, const):
								let uname = parentNames.get(field) ?? unique(name, field)
								if (const) type.staticVars.push(NiceStatement.Const(uname, nodeToExpression(expr), typer.types.get(field)))
								else type.staticVars.push(NiceStatement.Var(uname, nodeToExpression(expr), typer.types.get(field)))
							case Node.TFunction(name, expr, vars, rettype):
								let uname = parentNames.get(field) ?? unique(name, field)
								let a = []
								let defaults = []
								let types = []
								for (arg in vars) switch (arg) {
									case Node.TVar(name, t, expr, const):
										a.push(unique(name, arg))
										types.push(t)
										if (expr != null) defaults.push(nodeToExpression(expr))
										else defaults.push(null)
								}
								type.staticMethods.push(NiceStatement.Function(uname, unwrapNestingBlocks(nodeToStatement(expr)), a, defaults, typer.types.get(field)))
							case _: console.error("(internal warning) Unknown complex enum static field kind:", field)
						}
						case _: console.error("(internal warning) Unknown complex enum field kind:", f)
					}
					type.names = names
					type.constructors = constructors
					type.types = types
					project.enumsComplex.push(type)
					return null //type
				} else {
					let type = new NiceSimpleEnum()
					type.name = DataHelper.extractTypeName(t)
					let names = []
					let values = []
					for (f in fields) switch (f) {
						// `ValueName = b`
						case Node.TBinop(a, op, b): // op == OpAssign
							switch (a) {
								case Node.NodeTypeValue(t): switch (t) {
									case NodeType.Type(name):
										names.push(name)
								}
							}
							switch (b) {
								case Node.TString(s): values.push(NiceExpression.String(s))
								case Node.TInt(s): values.push(NiceExpression.Int(s))
								case _: console.error("(internal warning) Unknown node field value kind:", b)
							}
						// Just `ValueName`
						case Node.NodeTypeValue(t): switch (t) {
							case NodeType.Type(name):
								names.push(name)
						}
						case Node.TStatic(field):
							switch (field) {
							case Node.TVar(name, t, expr, const):
								let uname = parentNames.get(field) ?? unique(name, field)
								if (const) type.staticVars.push(NiceStatement.Const(uname, nodeToExpression(expr), typer.types.get(field)))
								else type.staticVars.push(NiceStatement.Var(uname, nodeToExpression(expr), typer.types.get(field)))
							case Node.TFunction(name, expr, vars, rettype):
								let uname = parentNames.get(field) ?? unique(name, field)
								let a = []
								let defaults = []
								let types = []
								for (arg in vars) switch (arg) {
									case Node.TVar(name, t, expr, const):
										a.push(unique(name, arg))
										types.push(t)
										if (expr != null) defaults.push(nodeToExpression(expr))
										else defaults.push(null)
								}
								type.staticMethods.push(NiceStatement.Function(uname, unwrapNestingBlocks(nodeToStatement(expr)), a, defaults, typer.types.get(field)))
							case _: console.error("(internal warning) Unknown simple enum static field kind:", field)
						}
						case _: console.error("(internal warning) Unknown simple enum field kind:", f)
					}
					type.names = names
					type.values = values
					project.enumsSimple.push(type)
					return null //type
				}

				case Node.TClass(t, ext, impl, fields, external):
				renameClassFields(fields)
				let type = new NiceClass()

				switch (typer.types.get(e)) {
					case Type.Class(the): type.useless = the.useless
				}

				type.external = external
				type.extend = DataHelper.extractTypeName(ext)

				for (implement in impl) switch (implement) {
					case NodeType.Type(name):
						type.implement = name
					case NodeType.ParametricType(name, params):
						type.implement = name
						type.implementParams = [for(param in params) DataHelper.extractTypeName(param)]
				}

				switch (t) {
					case NodeType.Type(name): // this name
					case NodeType.ParametricType(name, params):
						type.genericParams = [for(param in params) DataHelper.extractTypeName(param)]
				}

				let atts = Project.mapAttributes.get(e)

				let require = getAtt(atts, 'require')
				if (require != null) switch (require.values[0]) {
					case Node.TString(s): type.jsRequire = s
					case _: throw '@require takes string as argument'
				}

				let native = getAtt(atts, 'native')
				if (native != null) switch (native.values[0]) {
					case Node.TString(s):
						type.jsNative = s
						type.name = unique(s, e)
					case _: throw '@native takes string as argument'
				} else type.name = unique(DataHelper.extractTypeName(t), e)
				globals.push(type.name)

				for (f in fields) switch (f) {
					case Node.TVar(name, t, expr, const):
						let uname = parentNames.get(f) ?? unique(name, f)
						if (const) type.vars.push(NiceStatement.Const(uname, nodeToExpression(expr), typer.types.get(f)))
						else type.vars.push(NiceStatement.Var(uname, nodeToExpression(expr), typer.types.get(f)))
					case Node.TProperty(field, _, _):
						switch (field) {
							case Node.TVar(name, t, expr, const):
								let uname = parentNames.get(f) ?? unique(name, f)
								if (const) type.vars.push(NiceStatement.Const(uname, nodeToExpression(expr), typer.types.get(field)))
								else type.vars.push(NiceStatement.Var(uname, nodeToExpression(expr), typer.types.get(field)))
								type.property.push(uname)
						}
					case Node.TFunction(name, expr, vars, rettype):
						let uname = (name == 'new')? 'new' : (parentNames.get(f) ?? unique(name, f))
						let a = []
						let defaults = []
						let types = []
						for (arg in vars) switch (arg) {
							case Node.TVar(name, t, expr, const):
								a.push(unique(name, arg))
								types.push(t)
								if (expr != null) defaults.push(nodeToExpression(expr))
								else defaults.push(null)
						}
						type.methods.push(NiceStatement.Function(uname, unwrapNestingBlocks(nodeToStatement(expr)), a, defaults, typer.types.get(f)))
					case Node.TStatic(field):
						switch (field) {
							case Node.TVar(name, t, expr, const):
								let uname = parentNames.get(field) ?? unique(name, field)
								if (const) type.staticVars.push(NiceStatement.Const(uname, nodeToExpression(expr), typer.types.get(field)))
								else type.staticVars.push(NiceStatement.Var(uname, nodeToExpression(expr), typer.types.get(field)))
							case Node.TFunction(name, expr, vars, rettype):
								let uname = parentNames.get(field) ?? unique(name, field)
								let a = []
								let defaults = []
								let types = []
								for (arg in vars) switch (arg) {
									case Node.TVar(name, t, expr, const):
										a.push(unique(name, arg))
										types.push(t)
										if (expr != null) defaults.push(nodeToExpression(expr))
										else defaults.push(null)
								}
								type.staticMethods.push(NiceStatement.Function(uname, unwrapNestingBlocks(nodeToStatement(expr)), a, defaults, typer.types.get(field)))
							case _: console.error("(internal warning) Unknown class static field kind:", field)
						}
					case _: console.error("(internal warning) Unknown class field kind:", f)
				}
				project.classes.push(type)
				return null //type

				case Node.TTypeAlias(_):

				// Expressions
				case Node.TNew(path, t, args, fields, values, _):
				let v = NiceExpression.Ident(parentNames.get(typer.parents.get(e)) ?? DataHelper.extractTypeName(t))
				let a = []
				for (arg in args) {
					a.push(nodeToExpression(arg))
				}
				let result = NiceStatement.New(v, a)
				if (fields.length > 0) {
					let temp = unique('temp')
					let block = [NiceStatement.Const(temp, NiceExpression.New(v, a))]
					for (field in fields.length)
						block.push(NiceStatement.Assign(NiceExpression.Dot(NiceExpression.Ident(temp), fields[field]), nodeToExpression(values[field])))
					return NiceStatement.Block(block)
				}
				return result

				case Node.TCall(e, args, argNames):
				// `super()` in `new(){}`
				switch (e) {
					case Node.TSuper(_):
						let a = []
						for (arg in args) {
							a.push(nodeToExpression(arg))
						}
						return NiceStatement.SuperConstructor(a)
				}

				let v = nodeToExpression(e)
				let a = []
				for (arg in args) {
					a.push(nodeToExpression(arg))
				}

				return NiceStatement.Call(v, a)

				case Node.TVar(name, t, expr, const, external):
				let uname = parentNames.get(e) ?? unique(name, e)
				if (external) return null
				if (const) return NiceStatement.Const(uname, nodeToExpression(expr), typer.types.get(e))
				return NiceStatement.Var(uname, nodeToExpression(expr), typer.types.get(e))

				case Node.TFunction(name, expr, vars, rettype, external):
				let uname = name!=null?(parentNames.get(e) ?? unique(name, e)):null
				if (external) return null
				var rettype = rettype
				let a = []
				let defaults = []
				var types = []
				for (arg in vars) switch (arg) {
					case Node.TVar(name, t, expr, const):
						a.push(unique(name, arg))
						types.push(t)
						if (expr != null) defaults.push(nodeToExpression(expr))
						else defaults.push(null)
				}
				if (name == 'new') return NiceStatement.Function(null, unwrapNestingBlocks(nodeToStatement(expr)), a, defaults, typer.types.get(e))
				return NiceStatement.Function(uname, unwrapNestingBlocks(nodeToStatement(expr)), a, defaults, typer.types.get(e))

				case Node.TFor(name, over, by, range):
				if (range == null) switch (over) {
					case Node.TInt(s):
					let i = unique(name, e)
					// This allows to call `continue` within loop body,
					// otherwise infinite loop happens
					let it = unique('it')
					return NiceStatement.Block([
						NiceStatement.Var(i, NiceExpression.Int(0), typer.typeInt),
						NiceStatement.Var(it, NiceExpression.Int(0), typer.typeInt),
						NiceStatement.While(
							NiceExpression.Binop(NiceExpression.Ident(it), Token.OpLt, NiceExpression.Int(s)),
							NiceStatement.Block([
								NiceStatement.Assign(NiceExpression.Ident(i), NiceExpression.Ident(it)),
								NiceStatement.Increment(NiceExpression.Ident(it)),
								nodeToStatement(by)
							]),
							true)
					])
					case _: // Iterate over non-constant Int:

					var iteratorOverInt = false
					switch (typer.types.get(over)) {
						case Type.ClassInstance(type, generic):
							if (generic == null || generic.length == 0, type.name == 'Int')
								iteratorOverInt = true
					}

					if (iteratorOverInt) {
						let i = unique(name, e)
						let it = unique('it')
						let finish = unique('at')

						return NiceStatement.Block([
							NiceStatement.Var(i, NiceExpression.Int(0), typer.typeInt),
							NiceStatement.Var(it, NiceExpression.Int(0), typer.typeInt),
							NiceStatement.Var(finish, nodeToExpression(over), typer.types.get(over)),
							NiceStatement.While(
								NiceExpression.Binop(NiceExpression.Ident(it), Token.OpLt, NiceExpression.Ident(finish)),
								NiceStatement.Block([
									NiceStatement.Assign(NiceExpression.Ident(i), NiceExpression.Ident(it)),
									NiceStatement.Increment(NiceExpression.Ident(it)),
									nodeToStatement(by)
								]),
								true)
						])
					}
				}
				if (let range = range) {
					/*
						let i = 0
						let it = over
						let finish = range

						while (it < finish) {
							i = it
							it++
							by
						}
					*/

					let i = unique(name, e)
					let it = unique('it')
					let finish = unique('at')

					return NiceStatement.Block([
						NiceStatement.Var(i, NiceExpression.Int(0), typer.typeInt),
						NiceStatement.Var(it, nodeToExpression(over), typer.typeInt),
						NiceStatement.Var(finish, nodeToExpression(range), typer.typeInt),
						NiceStatement.While(
							NiceExpression.Binop(NiceExpression.Ident(it), Token.OpLt, NiceExpression.Ident(finish)),
							NiceStatement.Block([
								NiceStatement.Assign(NiceExpression.Ident(i), NiceExpression.Ident(it)),
								NiceStatement.Increment(NiceExpression.Ident(it)),
								nodeToStatement(by)
							]),
							true)
					])
				}
				return NiceStatement.For(name, nodeToExpression(over), nodeToStatement(by))

				case Node.TReturn(e):
				if (e == null) return NiceStatement.Return(null)
				return NiceStatement.Return(nodeToExpression(e))

				case Node.TDot(expr, name): return NiceStatement.Dot(nodeToExpression(expr), name)
				case Node.TDotUpper(expr, name): return NiceStatement.Dot(nodeToExpression(expr), name)

				case Node.TIf(econd, eif, eelse):
				// Check if it contains if-let bindings
				var binds = false
				for (cond in econd) switch (cond) {
					case Node.TVar(_):
					binds = true
				}

				// Just plain `if`
				if (!binds && econd.length == 1)
				return NiceStatement.If(nodeToExpression(econd[0]), nodeToStatement(eif), nodeToStatement(eelse))

				if (!binds) {
					var parens = NiceExpression.Parenthesis(nodeToExpression(econd[0]))
					var i = 1
					while (i < econd.length) {
						parens = NiceExpression.Binop(parens, Token.OpBoolAnd, NiceExpression.Parenthesis(nodeToExpression(econd[i])))
						i++
					}
					return NiceStatement.If(parens, nodeToStatement(eif), nodeToStatement(eelse))
				}

				// if(let, let, let...)
				let block = []
				var current = 0
				// Builds kind of state machine here
				let step = unique('step')
				block.push(NiceStatement.Var(step, NiceExpression.Int(0), typer.typeInt))
				var nullable = false
				var last = ''
 				switch (econd[0]) {
					case Node.TVar(name, t, expr, const):
					last = unique(name, econd[0])
					nullable = true
					block.push(NiceStatement.Const(last, nodeToExpression(expr), t))
					case _:
					last = unique('temp')
					block.push(NiceStatement.Const(last, nodeToExpression(econd[0])))
				}

				while (current < econd.length - 1) switch (econd[current + 1]) {
					case Node.TVar(vname, t, expr, const):
					let name = unique(vname, econd[current + 1])
					block.push(NiceStatement.Var(name, NiceExpression.Null))
					block.push(NiceStatement.If(
						NiceExpression.Binop(
							NiceExpression.Binop(NiceExpression.Ident(step), Token.OpEq, NiceExpression.Int(current + 0)),
							Token.OpBoolAnd,
							NiceExpression.Binop(NiceExpression.Ident(last), Token.OpNotEq, nullable? NiceExpression.Null : NiceExpression.False)
						),
						NiceStatement.Block([
							NiceStatement.Assign(NiceExpression.Ident(name), nodeToExpression(expr)),
							NiceStatement.Assign(NiceExpression.Ident(step), NiceExpression.Int(current + 1)), null
						]), null))
					current++
					last = name
					nullable = true
					case _:
					let next = unique('temp')
					block.push(NiceStatement.Var(next, NiceExpression.Null))
					block.push(NiceStatement.If(
						NiceExpression.Binop(
							NiceExpression.Binop(NiceExpression.Ident(step), Token.OpEq, NiceExpression.Int(current + 0)),
							Token.OpBoolAnd,
							NiceExpression.Binop(NiceExpression.Ident(last), Token.OpNotEq, nullable? NiceExpression.Null : NiceExpression.False)
						),
						NiceStatement.Block([
							NiceStatement.Assign(NiceExpression.Ident(next), nodeToExpression(econd[current + 1])),
							NiceStatement.Assign(NiceExpression.Ident(step), NiceExpression.Int(current + 1)), null
						]), null))
					current++
					last = next
					nullable = false
				}

				// Last step
				block.push(NiceStatement.If(
					NiceExpression.Binop(
						NiceExpression.Binop(NiceExpression.Ident(step), Token.OpEq, NiceExpression.Int(econd.length - 1)),
						Token.OpBoolAnd,
						NiceExpression.Binop(NiceExpression.Ident(last), Token.OpNotEq, nullable? NiceExpression.Null : NiceExpression.False)
					),
					NiceStatement.Assign(NiceExpression.Ident(step), NiceExpression.Int(econd.length)), null))

				// if (step == steps)
				block.push(NiceStatement.If(
					NiceExpression.Binop(NiceExpression.Ident(step), Token.OpEq, NiceExpression.Int(econd.length)),
					nodeToStatement(eif), nodeToStatement(eelse)))

				return NiceStatement.Block(block)

				case Node.TTry(expr, t, v, catches):
				var temp = unique('temp') // Used for `catch(temp)` in JS
				var tt = []
				var vv = []
				for (c in v) vv.push(DataHelper.nameOf(c))
				var cc = []
				for (c in catches) cc.push(nodeToStatement(c))
				for (c in t) tt.push(typer.types.get(c))
				return NiceStatement.Try(nodeToStatement(expr), tt, vv, cc, temp)

				case Node.TThrow(value):
				return NiceStatement.Throw(nodeToExpression(value))

				case Node.TAssignop(a, op, b):
				return NiceStatement.Assignop(nodeToExpression(a), op, nodeToExpression(b))

				case Node.TBinop(a, op, b):
				switch (op) {
					case Token.OpAssign:
					return NiceStatement.Assign(nodeToExpression(a), nodeToExpression(b))
					case _:
					let sa = nodeToStatement(a)
					let sb = nodeToStatement(b)
					if (sa == null, sb == null) return null
					if ((sa == null) || (sb == null)) return sa ?? sb
					return NiceStatement.Statements([sa, sb])
				}

				case Node.TContinue: return NiceStatement.Continue
				case Node.TBreak: return NiceStatement.Break

				case Node.TWhile(econd, e, pre):
				return NiceStatement.While(nodeToExpression(econd), nodeToStatement(e), pre)

				case Node.TUnop(op, postfix, e):
					switch (op) {
						case Token.OpIncrement: return NiceStatement.Increment(nodeToExpression(e))
						case Token.OpDecrement: return NiceStatement.Decrement(nodeToExpression(e))
						case _: // Optimize away
					}
				case Node.TParenthesis(inner):
				function unwrapNestedParens(inner: Node) {
					switch (inner) {
						case Node.TParenthesis(i): return unwrapNestedParens(i)
						case _: return inner
					}
				}
				return nodeToStatement(unwrapNestedParens(inner))

				case Node.TSwitch(exprs, conds, guards, cases):
				if (exprs.length > 1) console.error('Multiple switch values not supported:', e, Project.data.get(e))
				let s = []
				let c = []

				if (nativeEnums) {
					let index = nodeToExpression(exprs[0])
					for (i in cases.length) {

						function toCase(co) switch (co) {
							case Node.TNull(_): return NiceExpression.Null
							case Node.TUnderscore(_): return NiceExpression.Underscore
							case Node.TString(v): return NiceExpression.String(v)
							case Node.TInt(v): return NiceExpression.Int(v)
							case Node.TFloat(v): return NiceExpression.Float(v)
							case Node.TBool(v):
								if (v) return NiceExpression.True
								return NiceExpression.False
							case Node.TDot(expr, name): return NiceExpression.Dot(nodeToExpression(expr), name)
							case Node.TDotUpper(expr, name): return NiceExpression.Dot(nodeToExpression(expr), name)
							case Node.TBinop(a, op, b): switch (op) {
							case Token.OpOr: return NiceExpression.Binop(toCase(a), Token.OpOr, toCase(b))
								case _: console.error('(internal warning) Unknown native case binop kind:', e, typer.parents.get(e))
							}
							case Node.TCall(e, args, argNames): switch (e) {
								case Node.TDotUpper(expr, name):
									let a = []
									for (arg in args.length) switch (args[arg]) {
										case Node.TIdent(name): a.push(NiceExpression.Ident(unique(name, args[arg])))
										case Node.TUnderscore: a.push(NiceExpression.Underscore)
										case _: console.log('(internal warning) Unknown native case extract value kind:', args[arg])
									}
									return NiceExpression.Call(nodeToExpression(e), a)
								case _: console.error('(internal warning) Unknown case extract kind:', e, typer.parents.get(e))
							}
							case _:
							console.error('(internal warning) Unknown native enum case kind:', co, Project.data.get(co))
						}

						c.push([toCase(conds[i])])
						s.push(nodeToStatement(cases[i]))
					}
					return NiceStatement.Switch(index, c, s)
				}

				let temp = unique('temp')
				let value = nodeToExpression(exprs[0])
				var index = NiceExpression.Ident(temp)
				var promoted = false

				switch (typer.types.get(exprs[0])) {
					case Type.Enum(type):
						if (type.fieldsTypeSimple == null) { index = NiceExpression.EnumTag(index) promoted = true }
					case Type.EnumInstance(type):
						if (type.fieldsTypeSimple == null) { index = NiceExpression.EnumTag(index) promoted = true }
				}

				var i = 0
				for (ec in cases) {
					let block = []
					let co = conds[i]
					let cx = []

					function addTag(co) switch (co) {
						case Node.TNull(_): cx.push(NiceExpression.Null)
						case Node.TUnderscore(_):
						case Node.TString(v): cx.push(NiceExpression.String(v))
						case Node.TInt(v): cx.push(NiceExpression.Int(v))
						case Node.TFloat(v): cx.push(NiceExpression.Float(v))
						case Node.TBool(v):
							if (v) cx.push(NiceExpression.True)
							else cx.push(NiceExpression.False)
						case Node.TCall(e, args, argNames): switch (e) {
							case Node.TDotUpper(expr, name):
								let parent = typer.parents.get(expr)
								let tag = getTag(parent, name)
								cx.push(tag)
							// Promote to complex enum
							if (!promoted)
								index = NiceExpression.EnumTag(index)
							promoted = true
							for (arg in args.length) switch (args[arg]) {
								case Node.TIdent(name): block.push(NiceStatement.Const(unique(name, args[arg]), NiceExpression.Index(NiceExpression.Ident(temp), NiceExpression.Int(2 + arg))))
								case Node.TUnderscore: {}
								case _: console.log('(internal warning) Unknown case extract value kind:', arg)
							}
							case _: console.error('(internal warning) Unknown case extract kind:', e, typer.parents.get(e))
						}
						case Node.TDotUpper(expr, name):
							cx.push(getTag(typer.parents.get(expr), name))
						case Node.TBinop(a, op, b): switch (op) {
							case Token.OpOr:
							addTag(a)
							addTag(b)
							case _: console.error('(internal warning) Unknown case binop kind:', e, typer.parents.get(e))
						}
						case _:
						console.error('(internal warning) Unknown case kind:', co, Project.data.get(co))
					}

					addTag(co)

					c.push(cx)
					block.push(unwrapNestingBlocks(nodeToStatement(ec)))
					s.push(unwrapNestingBlocks(NiceStatement.Block(block)))
					i++
				}

				return NiceStatement.Block([NiceStatement.Const(temp, value, typer.types.get(typer.parents.get(exprs[0]))), NiceStatement.Switch(index, c, s)])

				case Node.TVars(vars):
				let s = []
				for (v in vars) if (let st = nodeToStatement(v)) s.push(st)
				return NiceStatement.Statements(s)

				// Optimize away
				case Node.TIndex(v, index):
					Project.hints.push(new Hint(Hints.UnusedValue, 'Unused value of index `\(Node.stringify(e))`', Project.data.get(e)))
					// No need to compute constant index
					switch (index) {
						case Node.TString(_): return nodeToStatement(v)
						case Node.TInt(_): return nodeToStatement(v)
						case Node.TBool(_): return nodeToStatement(v)
						case Node.TFloat(_): return nodeToStatement(v)
					}
				return NiceStatement.Statements([nodeToStatement(v), nodeToStatement(index)])

				// Optimize away
				case Node.TIdent(name):
					Project.hints.push(new Hint(Hints.UnusedValue, 'Unused value `\(name)`', Project.data.get(e)))
				case Node.TArrow(_):
				case Node.TString(v):
					Project.hints.push(new Hint(Hints.UnusedValue, 'Unused string `"\(v)"`', Project.data.get(e)))
				case Node.TBool(v):
					Project.hints.push(new Hint(Hints.UnusedValue, 'Unused boolean `\(v)`', Project.data.get(e)))
				case Node.TInt(v):
					Project.hints.push(new Hint(Hints.UnusedValue, 'Unused number `\(v)`', Project.data.get(e)))
				case Node.TFloat(v):
					Project.hints.push(new Hint(Hints.UnusedValue, 'Unused float `\(v)`', Project.data.get(e)))
				case Node.TNull:
					Project.hints.push(new Hint(Hints.UnusedValue, 'Unused `null`', Project.data.get(e)))
				case _:
					return NiceStatement.UnusedExpression(nodeToExpression(e))
			}

			return null
		}

		function getTag(enumNode: Node, name: String) {
			switch (enumNode) {
				case Node.TEnum(t, fields, valuesType):
				var typed: EnumType = null
				switch (typer.types.get(enumNode)) {
					case Type.Enum(type): typed = type
				}

				var i = 0
				for (f in fields) {
					switch (f) {
						case Node.NodeTypeValue(t): switch (t) {
							case NodeType.Type(n):
								if (n == name)
									if (typed.fieldsTypeSimple == typer.typeString)
										return NiceExpression.String(n)
								if (n == name) return NiceExpression.Int(i)
						}
						case Node.TCall(e, _): switch (e) {
							case Node.NodeTypeValue(t): switch (t) {
								case NodeType.Type(n):
									if (n == name) return NiceExpression.Int(i)
							}
						}
						case Node.TBinop(a, op, b): switch (a) {
							case Node.NodeTypeValue(t): switch (t) {
								case NodeType.Type(n):
									if (n == name) return nodeToExpression(b)
							}
						}
						case Node.TStatic(_): return null // Not a tag
						case _:
							console.error('(internal warning) Unknown enum tag kind:', f)
					}
					i++
				}
				throw 'Enum has no tag or field `\(name)` ' + t
			}
		}

		function getAtt(atts: [Attribute], atname: String): Attribute {
			if (atts != null && atts.length > 0) for (att in atts) {
				if (att.name == atname) return att
			}
			return null
		}

		let scopes: [[String : Bool]] = [[:]]
		let globals: [String] = [] // Type names are global

		// Randomization for the un-hack-ability
		var id = Math.round(Math.random() * 123.0)

		function pushScope() {
			scopes.push(([:]))
		}

		function popScope() {
			scopes.pop()
		}

		function hasInScope(name: String) {
			for (scope in scopes) {
				if (scope.get(name) != null) {
					return true
				}
			}
			return false
		}

		function addToScope(name: String) {
			scopes[scopes.length-1].set(name, true)
		}

		// Allows to find renamed node
		let parentNames: [Node : String] = [:]

		// Returns new name if name occupied or reserved
		// Returns same name and calls addToScope if name is free
		function unique(name: String, node: Node = null): String {
			var uname = name
			while (
				hasInScope(uname) ||
				(keywords.indexOf(uname) != -1) ||
				(globals.indexOf(uname) != -1)
			) uname = name + '_' + (++id)
			addToScope(uname)
			if (node != null) parentNames.set(node, uname)
			return uname
		}

		// Sets name without renaming for `@native`
		function uniqueNative(name: String, node: Node) {
			addToScope(name)
			parentNames.set(node, name)
		}

		function fillStatement(e: Node): NiceStatement {
			return nodeToStatement(e)
		}

		function renameClassFields(fields: [Node]) {
			pushScope()
			for (f in fields) switch (f) {
				case Node.TVar(name, t, expr, const):
					let atts = Project.mapAttributes.get(f)

					let native = getAtt(atts, 'native')
					if (native != null) switch (native.values[0]) {
						case Node.TString(s): if (parentNames.get(f) == null) unique(s, f)
						case _: throw '@native takes string as argument'
					} else
					if (parentNames.get(f) == null) unique(name, f)
				case Node.TFunction(name, expr, vars, rettype):
					if (name == 'new') parentNames.set(f, 'new')
					else {
						let atts = Project.mapAttributes.get(f)

						let native = getAtt(atts, 'native')
						if (native != null) switch (native.values[0]) {
							case Node.TString(s): if (parentNames.get(f) == null) unique(s, f)
							case _: throw '@native takes string as argument'
						} else

						if (parentNames.get(f) == null) unique(name, f)
					}
				case Node.TStatic(field): switch (field) {
					case Node.TVar(name, t, expr, const):
						if (parentNames.get(field) == null) unique(name, field)
					case Node.TFunction(name, expr, vars, rettype):
						let atts = Project.mapAttributes.get(f)

						let native = getAtt(atts, 'native')
						if (native != null) switch (native.values[0]) {
							case Node.TString(s): if (parentNames.get(field) == null) unique(s, field)
							case _: throw '@native takes string as argument'
						} else

						if (parentNames.get(field) == null) unique(name, field)
				}
			}
			popScope()
		}

		// Used for `something.name`
		function getClassDotField(expr: Node, name: String): String? {
			switch (expr) {
				case Node.TClass(t, ext, impl, fields, external): for (f in fields) switch (f) {
					case Node.TStatic(sf):
					if (DataHelper.nameOf(sf) == name)
					return parentNames.get(sf) ?? name
					case Node.TVar(n, _):
					if (n == name)
					return parentNames.get(f) ?? name
				}
				case Node.TEnum(t, fields, valuesType): for (f in fields) switch (f) {
					case Node.TStatic(sf):
					if (DataHelper.nameOf(sf) == name)
					return parentNames.get(sf) ?? name
				}
			}
			return null
		}

		// Converts `{ { { ... } } }` into just `...`
		function unwrapNestingBlocks(block) switch (block) {
			case NiceStatement.Block(el):
				if (el.length == 1) return unwrapNestingBlocks(el[0])
				return block
			case _:
				return block
		}
	}
}
