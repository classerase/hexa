// The Hexa Compiler
// Copyright (C) 2018  Oleg Petrenko
//
// This library is free software; you can redistribute it and/or
// modify it under the terms of the GNU Lesser General Public
// License as published by the Free Software Foundation; either
// version 2.1 of the License, or (at your option) any later version.
//
// This library is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
// Lesser General Public License for more details.
//
// You should have received a copy of the GNU Lesser General Public
// License along with this library; if not, write to the Free Software
// Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301  USA

module {
	// Generates simplified view of syntax tree
	// for easier later generation
	class Normalizer {
		static function normalize(collected: Node, keywords: [String], nativeEnums: Bool = false): NiceProject {
			return (new Normalizer(collected, keywords, nativeEnums)).project
		}

		let project = new NiceProject()
		var keywords = []
		var nativeEnums = false

		new (collected: Node, keywords: [String], nativeEnums) {
			this.keywords = keywords
			this.nativeEnums = nativeEnums
			switch (collected) {
			case Node.TBlock(el): // File
				// Rename classes upfront to avoid race condition
				for (e in el) switch (e) {
					case Node.TModule(path, el):
						for (e in el) switch (e) {
							case Node.TClass(t, ext, impl, fields, external):
							renameClassFields(fields)
							case Node.TEnum(t, fields, valuesType):
							renameClassFields(fields)
						}
				}

				// Fill statements
				for (e in el) switch (e) {
					case Node.TModule(path, el):
						for (e in el) fillStatement(e)
					case _:
					let statement = fillStatement(e)
					if (statement != null) switch (statement) {
						case NiceStatement.Block(el):
						if (el.length > 0) // Don't output empty blocks
						project.init.push(statement)
						case _: project.init.push(statement)
					}
				}
			}
		}

		function nodeToExpression(e: Node): NiceExpression {
			if (e == null) return null

			switch (e) {
				// Transformation not required
				case Node.TString(v): return NiceExpression.String(v)
				case Node.TInt(v): return NiceExpression.Int(v)
				case Node.TFloat(v): return NiceExpression.Float(v)
				case Node.TBool(v): return NiceExpression.Bool(v)
				case Node.TNull: return NiceExpression.Null

				// Transform
				case Node.TIdent(name):
					var source = Project.mapNames.get(e)
					switch (source) {
						case null:
							throw 'Unmapped \(e) ' + JSON.stringify(Project.data.get(e))
						case Node.TVar(name, _, _):
							if (Project.mapNames.get(source) != null)
							return NiceExpression.Dot(NiceExpression.This, name)
						case Node.TFunction(name, _):
							if (Project.mapNames.get(source) != null)
							return NiceExpression.Dot(NiceExpression.This, name)
						case Node.TStatic(f):
							let staticSource = Project.mapNames.get(source)
							switch (staticSource) {
								case Node.TClass(t, _):
									return NiceExpression.Dot(NiceExpression.Ident(DataHelper.extractTypeName(t)), getClassDotField(staticSource, name))
								case Node.TEnum(t,_):
									return NiceExpression.Dot(NiceExpression.Ident(DataHelper.extractTypeName(t)), name)
								case _: throw 'staticSource is \(staticSource)'
							}
					}
					return NiceExpression.Ident(parentNames.get(source) ?? name)
				case Node.TDot(expr, name):
				let parent = Project.mapNames.get(expr)
				switch (parent) {
					case Node.TEnum(t, fields, valuesType): if (valuesType != null) {
						return getTag(parent, name) ?? NiceExpression.Dot(nodeToExpression(expr), getClassDotField(parent, name) ?? name)
					} else return NiceExpression.Dot(nodeToExpression(expr), getClassDotField(parent, name) ?? name)
					case Node.TClass(_): return NiceExpression.Dot(nodeToExpression(expr), getClassDotField(parent, name) ?? name)
				}
				return NiceExpression.Dot(nodeToExpression(expr), name)

				case Node.TCall(e, args, argNames):

				// Optimize `'String'.charCodeAt(0)`
				if (args.length == 1) switch (e) {
					case Node.TDot(expr, name): if (name == 'charCodeAt') switch (expr) {
						case Node.TString(s): if (s.indexOf('\\') == -1) switch (args[0]) {
							case Node.TInt(i): if (i < s.length, i > -1) return NiceExpression.Int(s.charCodeAt(i))
						}
					}
				}

				let a = []
				for (arg in args) {
					a.push(nodeToExpression(arg))
				}
				let atts = Project.mapAttributes.get(Project.mapNames.get(e))

				let infix = getAtt(atts, 'infix')
				if (infix != null) switch (infix.values[0]) {
					case Node.TString(s): return NiceExpression.NativeOperator(NativeOperatorKind.Infix, a, s)
					case _: throw '@infix takes string as argument'
				}

				let operator = getAtt(atts, 'operator')
				if (operator != null) switch (operator.values[0]) {
					case Node.TString(s): return NiceExpression.NativeOperator(NativeOperatorKind.Function, a, s)
					case _: throw '@operator takes string as argument'
				}

				let v = nodeToExpression(e)
				return NiceExpression.Call(v, a)

				case Node.TBinop(a, op, b):
				let ea = nodeToExpression(a)
				let eb = nodeToExpression(b)

				if (op == Token.OpAdd) switch (ea) {
					case NiceExpression.String(sa):  switch (eb) {
						case NiceExpression.String(sb): return NiceExpression.String(sa + sb)
					}
				}

				return NiceExpression.Binop(ea, op, eb)

				case Node.TNew(path, t, args, fields, el):
				let v = NiceExpression.Ident(DataHelper.extractTypeName(t))
				let a = []
				for (arg in args) {
					a.push(nodeToExpression(arg))
				}
				return NiceExpression.New(v, a)

				case Node.TFunction(name, expr, vars, rettype):
				let a = []
				let defaults = []
				for (arg in vars) switch (arg) {
					case Node.TVar(name, t, expr, const):
						a.push(unique(name, arg))
						if (expr != null) defaults.push(nodeToExpression(expr))
						else defaults.push(null)
				}
				return NiceExpression.Function(name?unique(name, e):null, nodeToStatement(expr), a, defaults)

				case Node.TArrow(expr, vars, rettype):
				let a = []
				let defaults = []
				for (arg in vars) switch (arg) {
					case Node.TVar(name, t, expr, const):
						a.push(unique(name, arg))
						if (expr != null) defaults.push(nodeToExpression(expr))
						else defaults.push(null)
				}
				return NiceExpression.Arrow(nodeToExpression(expr), a, defaults)

				case Node.TBlock(el):
				let block = []
				var i = 0
				while (i < el.length - 1) {
					block.push(nodeToStatement(el[i]))
					i++
				}
				block.push(NiceStatement.Return(nodeToExpression(el[el.length - 1])))
				return NiceExpression.Call(NiceExpression.Parenthesis(NiceExpression.Function(null, NiceStatement.Block(block), [])), [])

				case Node.TObject(names, el):
				let els = []
				for (e in el) {
					els.push(nodeToExpression(e))
				}
				return NiceExpression.Object(names, els)

				case Node.TParenthesis(inner):
				function unwrapNestedParens(inner: Node) {
					switch (inner) {
						case Node.TParenthesis(i): return unwrapNestedParens(i)
						case _: return inner
					}
				}
				let value = unwrapNestedParens(inner)
				return NiceExpression.Parenthesis(nodeToExpression(value))

				case Node.TArray(el):
				if (el.length == 1) switch (el[0]) {
					case Node.TFor(name, over, by):
					let block = []
					let result = unique('result')
					block.push(NiceStatement.Const(result, NiceExpression.Array([])))
					let value = unique('value')
					block.push(NiceStatement.Const(value, nodeToExpression(over)))

					let push = NiceStatement.Call(NiceExpression.Dot(NiceExpression.Ident(result), 'push'), [nodeToExpression(by)])
					function fillLoop() {
						switch (over) {
							case Node.TDot(_, f): // TODO remove after proper type info
							if (f == "length") {
								let i = name
								block.push(NiceStatement.Var(i, NiceExpression.Int(-1)))
								block.push(NiceStatement.While(
									NiceExpression.Binop(
										NiceExpression.Parenthesis(NiceExpression.Binop(NiceExpression.Ident(i), Token.OpAdd, NiceExpression.Int(1))),
										Token.OpLt, NiceExpression.Ident(value)),
									NiceStatement.Block([
										NiceStatement.Increment(NiceExpression.Ident(i)),
										push
									]),
									true))
								return
							}

							case Node.TInt(s):
							let i = name
							block.push(NiceStatement.Const(i, NiceExpression.Int(-1)))
							block.push(NiceStatement.While(
								NiceExpression.Binop(
									NiceExpression.Parenthesis(NiceExpression.Binop(NiceExpression.Ident(i), Token.OpAdd, NiceExpression.Int(1))),
									Token.OpLt, NiceExpression.Ident(value)),
								NiceStatement.Block([
									NiceStatement.Increment(NiceExpression.Ident(i)),
									push
								]),
								true))
							return
						}

						block.push(NiceStatement.For(name, NiceExpression.Ident(value), push))
					}

					fillLoop()
					block.push(NiceStatement.Return(NiceExpression.Ident(result)))
					return NiceExpression.Call(NiceExpression.Parenthesis(NiceExpression.Function(null, NiceStatement.Block(block), [])), [])
				}
				let els = []
				for (e in el) els.push(nodeToExpression(e))
				return NiceExpression.Array(els)

				case Node.TElvis(nullable, othewise):
				return NiceExpression.Elvis(nodeToExpression(nullable), nodeToExpression(othewise))

				case Node.TUnop(op, postfix, e):
				return NiceExpression.Unop(op, postfix, nodeToExpression(e))

				case Node.TThis: return NiceExpression.This

				case Node.TMap(keys, values):
				let k = []
				let v = []
				for (key in keys) k.push(nodeToExpression(key))
				for (value in values) v.push(nodeToExpression(value))
				return NiceExpression.Map(k, v)

				case Node.TAssignop(a, op, b):
				return NiceExpression.Assignop(nodeToExpression(a), op, nodeToExpression(b))

				case Node.TIf(econd, eif, eelse):
				// Check if it contains if-let bindings
				var binds = false
				for (cond in econd) switch (cond) {
					case Node.TVar(_):
					binds = true
				}

				if (!binds && econd.length == 1)
				return NiceExpression.If(nodeToExpression(econd[0]), nodeToExpression(eif), nodeToExpression(eelse))

				if (!binds) {
					var parens = NiceExpression.Parenthesis(nodeToExpression(econd[0]))
					var i = 1
					while (i < econd.length) {
						parens = NiceExpression.Binop(parens, Token.OpBoolAnd, NiceExpression.Parenthesis(nodeToExpression(econd[i])))
						i++
					}
					return NiceExpression.If(parens, nodeToExpression(eif), nodeToExpression(eelse))
				}

				return NiceExpression.If(nodeToExpression(econd[0]), nodeToExpression(eif), nodeToExpression(eelse))

				case Node.TIndex(expr, index):
				return NiceExpression.Index(nodeToExpression(expr), nodeToExpression(index))

				case _: console.error('Unknown node type for expression:', e, Project.data.get(e))
			}

			return null
		}

		function nodeToStatement(e: Node): NiceStatement {
			if (e == null) return null

			switch (e) {
				case Node.TBlock(el):
				var els = [] // TODO break after: break, continue, return, throw
				for(e in el) {
					let statement = nodeToStatement(e)
					if (statement != null) // Optimized away
					els.push(statement)
				}
				return NiceStatement.Block(els)

				// Types
				case Node.TEnum(t, fields, valuesType):
				renameClassFields(fields)
				let isComplexEnum = valuesType == null
				if (isComplexEnum) {
					let type = new NiceComplexEnum()
					type.name = DataHelper.extractTypeName(t)
					let names = []
					let constructors = []
					let types = []
					for (f in fields) switch (f) {
						case Node.TIdent(name):
							names.push(name)
							constructors.push(null)
							types.push(null)
						case Node.TCall(e, args, argNames):
							switch (e) {
								case Node.TIdent(name): names.push(name)
							}
							constructors.push([for (arg in argNames) unique(arg)])
							types.push([for (arg in args) unique(arg)])
						case _: console.error("Unknown complex enum field kind:", f)
					}
					type.names = names
					type.constructors = constructors
					type.types = types
					project.enumsComplex.push(type)
					return null //type
				} else {
					let type = new NiceSimpleEnum()
					type.name = DataHelper.extractTypeName(t)
					let names = []
					let values = []
					for (f in fields) switch (f) {
						case Node.TBinop(a, op, b): // op == OpAssign
							switch (a) {
								case Node.TIdent(name): names.push(name)
							}
							switch (b) {
								case Node.TString(s): values.push(NiceExpression.String(s))
								case Node.TInt(s): values.push(NiceExpression.Int(s))
								case _: console.error("Unknown node field value kind:", b)
							}
						case Node.TStatic(field):
							switch (field) {
							case Node.TVar(name, t, expr, const):
								let uname = parentNames.get(field) ?? unique(name, field)
								if (const) type.staticVars.push(NiceStatement.Const(uname, nodeToExpression(expr)))
								else type.staticVars.push(NiceStatement.Var(uname, nodeToExpression(expr)))
							case Node.TFunction(name, expr, vars, rettype):
								let uname = parentNames.get(field) ?? unique(name, field)
								let a = []
								for (arg in vars) {
									a.push(unique(DataHelper.nameOf(arg), arg))
								}
								type.staticMethods.push(NiceStatement.Function(uname, unwrapNestingBlocks(nodeToStatement(expr)), a))
							case _: console.error("Unknown enum static field kind:", field)
						}
						case _: console.error("Unknown simple enum field kind:", f)
					}
					type.names = names
					type.values = values
					project.enumsSimple.push(type)
					return null //type
				}

				case Node.TClass(t, ext, impl, fields, external):
				renameClassFields(fields)
				let name = DataHelper.extractTypeName(t)
				let type = new NiceClass()
				type.name = name
				type.external = external

				let atts = Project.mapAttributes.get(e)

				let require = getAtt(atts, 'require')
				if (require != null) switch (require.values[0]) {
					case Node.TString(s): type.jsRequire = s
					case _: throw '@require takes string as argument'
				}

				let native = getAtt(atts, 'native')
				if (native != null) switch (native.values[0]) {
					case Node.TString(s): type.jsNative = s
					case _: throw '@native takes string as argument'
				}

				for (f in fields) switch (f) {
					case Node.TVar(name, t, expr, const):
						let uname = parentNames.get(f) ?? unique(name, f)
						if (const) type.vars.push(NiceStatement.Const(uname, nodeToExpression(expr)))
						else type.vars.push(NiceStatement.Var(uname, nodeToExpression(expr)))
					case Node.TFunction(name, expr, vars, rettype):
						let uname = (name == 'new')? 'new' : (parentNames.get(f) ?? unique(name, f))
						let a = []
						for (arg in vars) {
							a.push(unique(DataHelper.nameOf(arg), arg))
						}
						type.methods.push(NiceStatement.Function(uname, unwrapNestingBlocks(nodeToStatement(expr)), a))
					case Node.TStatic(field):
						switch (field) {
							case Node.TVar(name, t, expr, const):
								let uname = parentNames.get(field) ?? unique(name, field)
								if (const) type.staticVars.push(NiceStatement.Const(uname, nodeToExpression(expr)))
								else type.staticVars.push(NiceStatement.Var(uname, nodeToExpression(expr)))
							case Node.TFunction(name, expr, vars, rettype):
								let uname = parentNames.get(field) ?? unique(name, field)
								let a = []
								for (arg in vars) {
									a.push(unique(DataHelper.nameOf(arg), arg))
								}
								type.staticMethods.push(NiceStatement.Function(uname, unwrapNestingBlocks(nodeToStatement(expr)), a))
							case _: console.error("Unknown class static field kind:", field)
						}
					case _: console.error("Unknown class field kind:", f)
				}
				project.classes.push(type)
				return null //type

				case Node.TDeclare(name, t):
					switch (t) {
						case Node.TBinop(_):
						case Node.TFunction(_):
							Project.mapAttributes.set(t, Project.mapAttributes.get(e))
						case Node.TClass(_):
							// This is a bit hacky
							Project.mapAttributes.set(t, Project.mapAttributes.get(e))
							nodeToStatement(t)
						case Node.TVar(name, t, expr, const):
							Project.mapAttributes.set(t, Project.mapAttributes.get(e))
						case _: console.error("Unknown declare kind:", t)
					}

				// Expressions
				case Node.TNew(path, t, args, fields, el):
				let v = NiceExpression.Ident(DataHelper.extractTypeName(t))
				let a = []
				for (arg in args) {
					a.push(nodeToExpression(arg))
				}
				return NiceStatement.New(v, a)

				case Node.TCall(e, args, argNames):
				let v = nodeToExpression(e)
				let a = []
				for (arg in args) {
					a.push(nodeToExpression(arg))
				}
				return NiceStatement.Call(v, a)

				case Node.TVar(name, t, expr, const):
				if (const) return NiceStatement.Const(unique(name, e), nodeToExpression(expr))
				return NiceStatement.Var(unique(name, e), nodeToExpression(expr))

				case Node.TFunction(name, expr, vars, rettype):
				let a = []
				let defaults = []
				for (arg in vars) switch (arg) {
					case Node.TVar(name, t, expr, const):
						a.push(unique(name, arg))
						if (expr != null) defaults.push(nodeToExpression(expr))
						else defaults.push(null)
				}
				return NiceStatement.Function(name?unique(name, e):null, nodeToStatement(expr), a, defaults)
				if (name == 'new') return NiceStatement.Function(null, unwrapNestingBlocks(nodeToStatement(expr)), a, defaults)

				case Node.TFor(name, over, by): switch (over) {
					case Node.TDot(expr, f): // TODO remove after proper type info
					if (f == "length") {
						let i = name
						let v = unique('value')
						return NiceStatement.Block([
						NiceStatement.Const(v, nodeToExpression(over)),
						NiceStatement.Var(i, NiceExpression.Int(0)),
						NiceStatement.While(
							NiceExpression.Binop(NiceExpression.Ident(i), Token.OpLt, NiceExpression.Ident(v)),
							NiceStatement.Block([
								nodeToStatement(by),
								NiceStatement.Increment(NiceExpression.Ident(i))
							]),
							true)
						])
					}

					case Node.TInt(s):
					let i = name
					return NiceStatement.Block([
						NiceStatement.Var(i, NiceExpression.Int(0)),
						NiceStatement.While(
							NiceExpression.Binop(NiceExpression.Ident(i), Token.OpLt, NiceExpression.Int(s)),
							NiceStatement.Block([
								nodeToStatement(by),
								NiceStatement.Increment(NiceExpression.Ident(i))
							]),
							true)
					])
				}
				return NiceStatement.For(name, nodeToExpression(over), nodeToStatement(by))

				case Node.TReturn(e):
				if (e == null) return NiceStatement.Return(null)
				return NiceStatement.Return(nodeToExpression(e))

				case Node.TDot(expr, name): return NiceStatement.Dot(nodeToExpression(expr), name)

				case Node.TIf(econd, eif, eelse):
				// Check if it contains if-let bindings
				var binds = false
				for (cond in econd) switch (cond) {
					case Node.TVar(_):
					binds = true
				}

				if (!binds && econd.length == 1)
				return NiceStatement.If(nodeToExpression(econd[0]), nodeToStatement(eif), nodeToStatement(eelse))

				if (!binds) {
					var parens = NiceExpression.Parenthesis(nodeToExpression(econd[0]))
					var i = 1
					while (i < econd.length) {
						parens = NiceExpression.Binop(parens, Token.OpBoolAnd, NiceExpression.Parenthesis(nodeToExpression(econd[i])))
						i++
					}
					return NiceStatement.If(parens, nodeToStatement(eif), nodeToStatement(eelse))
				}

				// if(let, let, let...)
				let block = []
				var current = 0
				let step = unique('step')
				block.push(NiceStatement.Var(step, NiceExpression.Int(0)))
				var nullable = false
				var last = ''
 				switch (econd[0]) {
					case Node.TVar(name, t, expr, const):
					last = unique(name, econd[0])
					nullable = true
					block.push(NiceStatement.Const(last, nodeToExpression(expr)))
					case _:
					last = unique('temp')
					block.push(NiceStatement.Const(last, nodeToExpression(econd[0])))
				}

				while (current < econd.length - 1) switch (econd[current + 1]) {
					case Node.TVar(vname, t, expr, const):
					let name = unique(vname, econd[current + 1])
					block.push(NiceStatement.Var(name, NiceExpression.Null))
					block.push(NiceStatement.If(
						NiceExpression.Binop(
							NiceExpression.Binop(NiceExpression.Ident(step), Token.OpEq, NiceExpression.Int(current + 0)),
							Token.OpBoolAnd,
							NiceExpression.Binop(NiceExpression.Ident(last), Token.OpNotEq, nullable? NiceExpression.Null : NiceExpression.Bool(false))
						),
						NiceStatement.Block([
							NiceStatement.Assign(NiceExpression.Ident(name), nodeToExpression(expr)),
							NiceStatement.Assign(NiceExpression.Ident(step), NiceExpression.Int(current + 1)), null
						]), null))
					current++
					last = name
					nullable = true
					case _:
					let next = unique('temp')
					block.push(NiceStatement.Var(next, NiceExpression.Null))
					block.push(NiceStatement.If(
						NiceExpression.Binop(
							NiceExpression.Binop(NiceExpression.Ident(step), Token.OpEq, NiceExpression.Int(current + 0)),
							Token.OpBoolAnd,
							NiceExpression.Binop(NiceExpression.Ident(last), Token.OpNotEq, nullable? NiceExpression.Null : NiceExpression.Bool(false))
						),
						NiceStatement.Block([
							NiceStatement.Assign(NiceExpression.Ident(next), nodeToExpression(econd[current + 1])),
							NiceStatement.Assign(NiceExpression.Ident(step), NiceExpression.Int(current + 1)), null
						]), null))
					current++
					last = next
					nullable = false
				}

				// Last step
				block.push(NiceStatement.If(
					NiceExpression.Binop(
						NiceExpression.Binop(NiceExpression.Ident(step), Token.OpEq, NiceExpression.Int(econd.length - 1)),
						Token.OpBoolAnd,
						NiceExpression.Binop(NiceExpression.Ident(last), Token.OpNotEq, NiceExpression.Null)
					),
					NiceStatement.Assign(NiceExpression.Ident(step), NiceExpression.Int(econd.length)), null))

				// if (step == steps)
				block.push(NiceStatement.If(
					NiceExpression.Binop(NiceExpression.Ident(step), Token.OpEq, NiceExpression.Int(econd.length)),
					nodeToStatement(eif), nodeToStatement(eelse)))

				return NiceStatement.Block(block)

				case Node.TTry(expr, t, v, catches):
				var tt = []
				var vv = []
				for (c in v) vv.push(DataHelper.nameOf(c))
				var cc = []
				for (c in catches) cc.push(nodeToStatement(c))
				return NiceStatement.Try(nodeToStatement(expr), tt, vv, cc)

				case Node.TThrow(value):
				return NiceStatement.Throw(nodeToExpression(value))

				case Node.TAssignop(a, op, b):
				return NiceStatement.Assignop(nodeToExpression(a), op, nodeToExpression(b))

				case Node.TBinop(a, op, b):
				switch (op) {
					case Token.OpAssign:
					return NiceStatement.Assign(nodeToExpression(a), nodeToExpression(b))
					case _:
					let sa = nodeToStatement(a)
					let sb = nodeToStatement(b)
					if (sa == null, sb == null) return null
					if ((sa == null) || (sb == null)) return sa ?? sb
					return NiceStatement.Statements([sa, sb])
				}

				case Node.TContinue: return NiceStatement.Continue
				case Node.TBreak: return NiceStatement.Break

				case Node.TWhile(econd, e, pre):
				return NiceStatement.While(nodeToExpression(econd), nodeToStatement(e), pre)

				case Node.TUnop(op, postfix, e):
					switch (op) {
						case Token.OpIncrement: return NiceStatement.Increment(nodeToExpression(e))
						case Token.OpDecrement: return NiceStatement.Decrement(nodeToExpression(e))
						case _: // Optimize away
					}
				case Node.TParenthesis(inner):
				function unwrapNestedParens(inner: Node) {
					switch (inner) {
						case Node.TParenthesis(i): return unwrapNestedParens(i)
						case _: return inner
					}
				}
				return nodeToStatement(unwrapNestedParens(inner))

				case Node.TSwitch(exprs, conds, guards, cases):
				if (exprs.length > 1) console.error('Multiple switch values not supported:', e, Project.data.get(e))
				let s = []
				let c = []

				if (nativeEnums) {
					let index = nodeToExpression(exprs[0])
					for (i in cases.length) {

						function toCase(co) switch (co) {
							case Node.TNull(_): return NiceExpression.Null
							case Node.TUnderscore(_): return NiceExpression.Underscore
							case Node.TString(v): return NiceExpression.String(v)
							case Node.TInt(v): return NiceExpression.Int(v)
							case Node.TFloat(v): return NiceExpression.Float(v)
							case Node.TBool(v): return NiceExpression.Bool(v)
							case Node.TDot(expr, name): return nodeToExpression(co)
							case Node.TBinop(a, op, b): switch (op) {
							case Token.OpOr: return NiceExpression.Binop(toCase(a), Token.OpOr, toCase(b))
								case _: console.error('Unknown native case binop kind:', e, Project.mapNames.get(e))
							}
							case Node.TCall(e, args, argNames): switch (e) {
								case Node.TDot(expr, name):
									let a = []
									for (arg in args.length) switch (args[arg]) {
										case Node.TIdent(name): a.push(NiceExpression.Ident(unique(name, args[arg])))
										case Node.TUnderscore: a.push(NiceExpression.Underscore)
										case _: console.log('Unknown native case extract value kind:', args[arg])
									}
									return NiceExpression.Call(nodeToExpression(e), a)
								case _: console.error('Unknown case extract kind:', e, Project.mapNames.get(e))
							}
							case _:
							console.error('Unknown native enum case kind:', co, Project.data.get(co))
						}

						c.push([toCase(conds[i])])
						s.push(nodeToStatement(cases[i]))
					}
					return NiceStatement.Switch(index, c, s)
				}

				let temp = unique('temp')
				let value = nodeToExpression(exprs[0])
				var index = NiceExpression.Ident(temp)

				var i = 0
				for (ec in cases) {
					let block = []
					let co = conds[i]
					let cx = []

					function addTag(co) switch (co) {
						case Node.TNull(_): cx.push(NiceExpression.Null)
						case Node.TUnderscore(_):
						case Node.TString(v): cx.push(NiceExpression.String(v))
						case Node.TInt(v): cx.push(NiceExpression.Int(v))
						case Node.TFloat(v): cx.push(NiceExpression.Float(v))
						case Node.TBool(v): cx.push(NiceExpression.Bool(v))
						case Node.TCall(e, args, argNames): switch (e) {
							case Node.TDot(expr, name): cx.push(getTag(Project.mapNames.get(expr), name))
							// Promote to complex enum
							index = NiceExpression.Binop(NiceExpression.Ident(temp), Token.OpBoolAnd, NiceExpression.Index(NiceExpression.Ident(temp), NiceExpression.Int(1)))
							for (arg in args.length) switch (args[arg]) {
								case Node.TIdent(name): block.push(NiceStatement.Const(unique(name, args[arg]), NiceExpression.Index(NiceExpression.Ident(temp), NiceExpression.Int(2 + arg))))
								case Node.TUnderscore: {}
								case _: console.log('Unknown case extract value kind:', arg)
							}
							case _: console.error('Unknown case extract kind:', e, Project.mapNames.get(e))
						}
						case Node.TDot(expr, name):
							cx.push(getTag(Project.mapNames.get(expr), name))
						case Node.TBinop(a, op, b): switch (op) {
							case Token.OpOr:
							addTag(a)
							addTag(b)
							case _: console.error('Unknown case binop kind:', e, Project.mapNames.get(e))
						}
						case _:
						console.error('Unknown case kind:', co, Project.data.get(co))
					}

					addTag(co)

					c.push(cx)
					block.push(unwrapNestingBlocks(nodeToStatement(ec)))
					s.push(unwrapNestingBlocks(NiceStatement.Block(block)))
					i++
				}

				return NiceStatement.Block([NiceStatement.Const(temp, value), NiceStatement.Switch(index, c, s)])

				case Node.TVars(vars):
				let s = []
				for (v in vars) s.push(nodeToStatement(v))
				return NiceStatement.Statements(s)

				case Node.TIndex(v, index):
				return NiceStatement.Statements([nodeToStatement(v), nodeToStatement(index)])

				// Optimize away
				case Node.TIdent(name):
				case Node.TArrow(_):
				case Node.TString:
				case Node.TBool:
				case Node.TInt:
				case Node.TFloat:
				case Node.TNull:
				case _: console.error('Unknown node type for statement:', e, Project.data.get(e))
			}

			return null
		}

		function getTag(enumNode: Node, name: String) {
			switch (enumNode) {
				case Node.TEnum(t, fields, valuesType):
				var i = 0
				for (f in fields) {
					switch (f) {
						case Node.TIdent(n): if (n == name) return NiceExpression.Int(i)
						case Node.TCall(e, _): switch (e) {
							case Node.TIdent(n): if (n == name) return NiceExpression.Int(i)
						}
						case Node.TBinop(a, op, b): switch (a) {
							case Node.TIdent(n): if (n == name) return nodeToExpression(b)
						}
						case Node.TStatic(_): // Not a tag
						case _: console.error('Unknown enum tag kind:', f)
					}
					i++
				}
			}
		}

		function getAtt(atts: [Attribute], atname: String): Attribute {
			if (atts != null && atts.length > 0) for (att in atts) {
				if (att.name == atname) return att
			}
			return null
		}

		var scopes: [[String : Bool]] = [[:]]

		var id = 0

		function pushScope() {
			scopes.push(new Map())
		}

		function popScope() {
			scopes.pop()
		}

		function hasInScope(name: String) {
			for (scope in scopes) {
				if (scope.get(name) != null) {
					return true
				}
			}
			return false
		}

		function addToScope(name: String) {
			scopes[scopes.length-1].set(name, true)
		}

		// Allows to find renamed node
		let parentNames: [Node : String] = [:]

		// Returns new name if name occupied or reserved
		// Returns same name and calls addToScope if name is free
		function unique(name: String, node: Node): String {
			var uname = name
			while (hasInScope(uname) || (keywords.indexOf(uname) != -1)) uname = name + '_' + (++id)
			addToScope(uname)
			parentNames.set(node, uname)
			return uname
		}

		function fillStatement(e: Node): NiceStatement {
			return nodeToStatement(e)
		}

		function renameClassFields(fields: [Node]) {
			pushScope()
			for (f in fields) switch (f) {
				case Node.TVar(name, t, expr, const): if (parentNames.get(f) == null) unique(name, f)
				case Node.TFunction(name, expr, vars, rettype):
					if (name == 'new') parentNames.set(f, 'new')
					else if (parentNames.get(f) == null) unique(name, f)
				case Node.TStatic(field): switch (field) {
					case Node.TVar(name, t, expr, const): if (parentNames.get(field) == null) unique(name, field)
					case Node.TFunction(name, expr, vars, rettype): if (parentNames.get(field) == null) unique(name, field)
				}
			}
			popScope()
		}

		// Used for `something.name`
		function getClassDotField(expr: Node, name: String) {
			switch (expr) {
				case Node.TClass(t, ext, impl, fields, external): for (f in fields) switch (f) {
					case Node.TStatic(sf):
					if (DataHelper.nameOf(sf) == name)
					return parentNames.get(sf) ?? name
				}
				case Node.TEnum(t, fields, valuesType): for (f in fields) switch (f) {
					case Node.TStatic(sf):
					if (DataHelper.nameOf(sf) == name)
					return parentNames.get(sf) ?? name
				}
			}
		}

		// Converts `{ { { ... } } }` into just `...`
		function unwrapNestingBlocks(block) switch (block) {
			case NiceStatement.Block(el):
				if (el.length == 1) return unwrapNestingBlocks(el[0])
				return block
			case _:
				return block
		}
	}

	class NiceProject {
		let interfaces: [NiceInterface] = []
		let classes: [NiceClass] = []
		let enumsSimple: [NiceSimpleEnum] = []
		let enumsComplex: [NiceComplexEnum] = []
		let globalVars: [NiceVar] = []
		let globalFuncs: [NiceFunction] = []
		let init: NiceNode = []

		new () {}
	}

	class NiceClass {
		new () {}
		var external: Bool
		var name: String
		var extend: NiceClass
		var implement: NiceInterface
		var parent: NiceParentModule // ???
		var rename: Int
		var constructor

		var staticMethods = []
		var staticVars = []
		var methods = []
		var vars = []

		var jsRequire: String = null
		var jsNative: String = null
	}

	class NiceSimpleEnum {
		new () {}
		var name: String
		var external: Bool
		var names: [String]
		var values: [NiceExpression]
		var staticMethods = []
		var staticVars = []
	}

	class NiceComplexEnum  {
		new () {}
		var name: String
		var external: Bool
		var names: [String]
		var constructors: [[String]]
		var types: [[NodeType]]
	}

	// Does not return a value
	enum NiceStatement {
		Statements(el: [NiceStatement]) // Multiple statements not wrapped into a block
		Block(el: [NiceStatement])
		Call(e: NiceExpression, args: [NiceExpression])
		New(e: NiceExpression, args: [NiceExpression])
		Var(name: String, expr: NiceExpression)
		Const(name: String, expr: NiceExpression)
		Function(name: String, expr: NiceStatement, args: [String], defaults: [NiceExpression])
		Return(e: NiceExpression) // TODO Null?
		If(econd: NiceExpression, eif: NiceStatement, eelse: NiceStatement) // TODO Null?
		Try(expr: NiceExpression, t: [String], v: [String], catches: [NiceStatement])
		Throw(value: NiceExpression)
		Assign(name: NiceExpression, value: NiceExpression)
		Assignop(name: NiceExpression, op: Token, value: NiceExpression)
		Dot(expr: NiceExpression, name: String)
		For(name: String, over: NiceExpression, by: NiceStatement)
		While(econd: NiceExpression, e: NiceStatement, pre: Bool)
		Increment(e: NiceExpression)
		Decrement(e: NiceExpression)
		Switch(expr: NiceExpression, cases: [[NiceExpression]], statements: [NiceStatement])
		Continue
		Break
	}

	// Returns a value
	enum NiceExpression {
		String(s: String)
		Int(s: String)
		Float(s: String)
		Bool(s: Bool)
		Ident(s: String)
		Null
		This
		Dot(expr: NiceExpression, name: String)
		Call(e: NiceExpression, args: [NiceExpression])
		Enum(name: String, tag: String, args: [NiceExpression])
		New(e: NiceExpression, args: [NiceExpression])
		Binop(a: NiceExpression, op: Token, b: NiceExpression)
		Function(name: String, expr: NiceStatement, args: [String], defaults: [NiceExpression])
		Arrow(expr: NiceExpression, args: [String], defaults: [NiceExpression])
		Parenthesis(inner: NiceExpression)
		Object(names: [String], el: [NiceExpression])
		Array(el: [NiceExpression])
		Map(keys: [NiceExpression], values: [NiceExpression])
		Unop(op: Token, postfix: Bool, e: NiceExpression)
		Elvis(nullable: Node, othewise: Node)
		If(econd: NiceExpression, eif: NiceExpression, eelse: NiceExpression) // TODO Null?
		Index(expr: NiceExpression, index: NiceExpression)
		Assignop(name: NiceExpression, op: Token, value: NiceExpression)
		NativeOperator(kind: NativeOperatorKind, args: [NiceExpression], name: String)
		Underscore
	}

	enum NativeOperatorKind : Int {
		Infix = 0
		Function = 1
		Prefix = 2
		Postfix = 3
	}
}
